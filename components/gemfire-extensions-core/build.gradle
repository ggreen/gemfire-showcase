apply plugin: 'signing'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 11
version = '2.0.0-SNAPSHOT'

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

group = 'io.pivotal.services.dataTx'
archivesBaseName = 'gemfire-extensions-core'

ext {
    gemFireVersion = '10.0.0'
}

compileJava   {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}


task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts { archives sourcesJar, javadocJar }

signing {
    sign(publishing.publications)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'gemfire-extensions-core',
                'Can-Redefine-Classes': false,
                'Can-Set-Native-Method-Prefix': false
        )
    }
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url "https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire"
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
        credentials {
            username repoUsername
            password repoPassword
        }
    }
}
dependencies {
    implementation group: 'com.vmware.gemfire', name: 'gemfire-core', version: gemFireVersion
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'com.vmware.gemfire:gemfire-search:1.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.1'
    implementation group: 'com.vmware.gemfire', name: 'gemfire-common', version: gemFireVersion
    implementation group: 'com.vmware.gemfire', name: 'gemfire-cq', version: gemFireVersion
    implementation group: 'com.vmware.gemfire', name: 'gemfire-wan', version: gemFireVersion
    implementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    implementation group: 'com.github.nyla-solutions', name: 'nyla.solutions.core', version: '1.5.1'

    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.6.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    testImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.9'
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.0")

}
test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}


task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}


publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'gemfire-extensions-core'
                groupId = group
                artifactId = 'gemfire-extensions-core'
                description = 'This Java API provides support for GemFire'
                packaging = 'jar'
                url = 'https://github.com/ggreen/gemfire-extensions'
                licenses {
                    license {
                        url = 'https://github.com/ggreen/gemfire-extensions/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ggreen'
                        name = 'Gregory Green'
                        email = 'gregoryg@vmware.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ggreen/gemfire-extensions.git'
                    developerConnection = 'scm:git:https://github.com/ggreen/gemfire-extensions.git'
                    url = 'https://github.com/ggreen/gemfire-extensions.git'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "CentralMaven" //  optional target repository name
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

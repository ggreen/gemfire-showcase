apply plugin: 'signing'
apply plugin: 'java'
apply plugin: 'maven-publish'


sourceCompatibility = 11
version = '2.0.0'


allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

group = 'io.pivotal.services.dataTx'
archivesBaseName = 'gemfire-extensions-spring-security'


ext {
    gemFireVersion = '10.0.0'
}


compileJava   {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

task myJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}


task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts { archives sourcesJar, javadocJar }

signing {
    sign(publishing.publications)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'gemfire-extensions-core',
                'Can-Redefine-Classes': false,
                'Can-Set-Native-Method-Prefix': false
        )
    }
}

repositories {
    mavenCentral()
    mavenLocal()


    maven {
        url "https://packages.broadcom.com/artifactory/gemfire/"
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
        credentials {
            username System.getenv('BROADCOM_MAVEN_USERNAME')
            password System.getenv('BROADCOM_GEMFIRE_MAVEN_PASSWORD')
        }
    }
}

dependencies {

    implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.6.4'
    implementation group: 'com.vmware.gemfire', name: 'gemfire-core', version: gemFireVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.4'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.18.0'
    implementation group: 'com.github.nyla-solutions', name: 'nyla.solutions.core', version: '1.5.1'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'

}
test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = archivesBaseName
                groupId = group
                artifactId = archivesBaseName
                description = 'This Spring Security user detail services backed by GemFire'
                packaging = 'jar'
                url = 'https://github.com/ggreen/gemfire-extensions'
                licenses {
                    license {
                        url = 'https://github.com/ggreen/gemfire-extensions/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ggreen'
                        name = 'Gregory Green'
                        email = 'gregoryg@vmware.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ggreen/gemfire-extensions.git'
                    developerConnection = 'scm:git:https://github.com/ggreen/gemfire-extensions.git'
                    url = 'https://github.com/ggreen/gemfire-extensions.git'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "CentralMaven" //  optional target repository name
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv('BROADCOM_MAVEN_USERNAME')
                password System.getenv('BROADCOM_GEMFIRE_MAVEN_PASSWORD')
            }
        }
    }
}